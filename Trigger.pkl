abstract module com.github.action.Trigger

/// Define the type of activity that will trigger a workflow run.
///
/// Most GitHub events are triggered by more than one type of activity.
/// For example, the label is triggered when a label is created, edited, or deleted.
/// The types keyword enables you to narrow down activity that causes the workflow to run.
/// When only one activity type triggers a webhook event, the types keyword is unnecessary.
///
/// You can use an array of event types.
///
/// For more information about each event and their activity types, see
/// [Events that trigger workflows](https://docs.github.com/en/actions/reference/workflows-and-actions/events-that-trigger-workflows#available-events).
types: Listing<String>?

/// The base trigger, with no event types associated
class BaseTrigger extends module {
  types: Listing<nothing>?
}

class BranchProtectionRule extends module {
  types: Listing<BranchProtectionRuleType>?
}

typealias BranchProtectionRuleType = "created" | "edited" | "deleted"

class CheckRun extends module {
  types: Listing<CheckRunType>?
}

typealias CheckRunType = "created" | "rerequested" | "completed" | "requested_action"

class CheckSuite extends module {
  types: Listing<CheckSuiteType>?
}
typealias CheckSuiteType = "completed" | "requested" | "rerequested"

class Discussion extends module {
  types: Listing<DiscussionType>?
}
typealias DiscussionType =
  "created"
    | "edited"
    | "deleted"
    | "transferred"
    | "pinned"
    | "unpinned"
    | "labeled"
    | "unlabeled"
    | "locked"
    | "unlocked"
    | "category_changed"
    | "answered"
    | "unanswered"

class DiscussionComment extends module {
  types: Listing<DiscussionCommentType>?
}
typealias DiscussionCommentType = "created" | "edited" | "deleted"

class IssueComment extends module {
  types: Listing<IssueCommentType>?
}
typealias IssueCommentType = "created" | "edited" | "deleted"

class Issues extends module {
  types: Listing<IssuesType>?
}
typealias IssuesType =
  "opened"
    | "edited"
    | "deleted"
    | "transferred"
    | "pinned"
    | "unpinned"
    | "closed"
    | "reopened"
    | "assigned"
    | "unassigned"
    | "labeled"
    | "unlabeled"
    | "locked"
    | "unlocked"
    | "milestoned"
    | "demilestoned"

class Label extends module {
  types: Listing<LabelType>?
}
typealias LabelType = "created" | "edited" | "deleted"

class MergeGroup extends module {
  types: Listing<MergeGroupType>?
}
typealias MergeGroupType = "checked_requested"

class Milestone extends module {
  types: Listing<MilestoneType>?
}
typealias MilestoneType = "created" | "closed" | "opened" | "edited" | "deleted"

class Project extends module {
  types: Listing<ProjectType>?
}
typealias ProjectType = "created" | "updated" | "closed" | "reopened" | "edited" | "deleted"

class ProjectCard extends module {
  types: Listing<ProjectCardType>?
}
typealias ProjectCardType = "created" | "moved" | "converted" | "edited" | "deleted"

class ProjectColumn extends module {
  types: Listing<ProjectColumnType>?
}
typealias ProjectColumnType = "created" | "updated" | "moved" | "deleted"

class PullRequest extends module {
  types: Listing<PullRequestType>?

  /// Use the branches filter when you want to include branch name patterns or when you want to both include and
  /// exclude branch names patterns.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  ///
  /// See <https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#onpull_requestpull_request_targetbranchesbranches-ignore>
  branches: Listing<String>?

  /// Use the branches-ignore filter when you only want to exclude branch name patterns.
  ///
  /// You cannot use both the branches and branches-ignore filters for the same event in a workflow.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  ///
  /// See <https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#onpull_requestpull_request_targetbranchesbranches-ignore>
  `branches-ignore`: Listing<String>(branches == null)?

  /// Use the paths filter when you want to include file path patterns or when you want to both include and exclude
  /// file path patterns.
  ///
  /// Accepts glob patterns that use the `*` and `**` wildcard characters to match more than one path name.
  /// For more information, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  paths: Listing<String>?

  /// Use the paths-ignore filter when you only want to exclude file path patterns.
  ///
  /// You cannot use both the paths and paths-ignore filters for the same event in a workflow.
  /// If you want to both include and exclude path patterns for a single event, use the paths filter prefixed with the
  /// `!` character to indicate which paths should be excluded.
  ///
  /// Accepts glob patterns that use the `*` and `**` wildcard characters to match more than one path name.
  /// For more information, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  `paths-ignore`: Listing<String>(paths == null)?
}

typealias PullRequestType =
  "assigned"
    | "unassigned"
    | "review_requested"
    | "review_request_removed"
    | "labeled"
    | "unlabeled"
    | "opened"
    | "edited"
    | "closed"
    | "reopened"
    | "synchronize"
    | "ready_for_review"
    | "locked"
    | "unlocked"
    | "ready_for_review"
    | "converted_to_draft"
    | "demilestoned"
    | "milestoned"
    | "review_requested"
    | "review_request_removed"
    | "auto_merge_enabled"
    | "auto_merge_disabled"

class PullRequestReview extends module {
  types: Listing<PullRequestReviewType>?
}
typealias PullRequestReviewType = "submitted" | "edited" | "dismissed"

class PullRequestReviewComment extends module {
  types: Listing<PullRequestReviewCommentType>?
}
typealias PullRequestReviewCommentType = "created" | "edited" | "deleted"

typealias PullRequestTargetType =
  "assigned"
    | "unassigned"
    | "labeled"
    | "unlabeled"
    | "opened"
    | "edited"
    | "closed"
    | "reopened"
    | "synchronize"
    | "converted_to_draft"
    | "ready_for_review"
    | "locked"
    | "unlocked"
    | "review_requested"
    | "review_request_removed"
    | "auto_merge_enabled"
    | "auto_merge_disabled"

class Push extends module {
  /// Use the `branches` filter when you want to include branch name patterns or when you want to both include and
  /// exclude branch names patterns.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch
  /// or tag name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  branches: Listing<String>?

  /// Use the `branches-ignore` filter when you only want to exclude branch name patterns.
  ///
  /// You cannot use both the [branches] and [`branches-ignore`] filters for the same event in a workflow.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch
  /// or tag name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  `branches-ignore`: Listing<String>(branches == null)?

  /// Use the tags filter when you want to include tag name patterns or when you want to both include and exclude tag
  /// names patterns.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch
  /// or tag name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  tags: Listing<String>?

  /// Use the tags-ignore filter when you only want to exclude tag name patterns.
  /// You cannot use both the [tags] and [tags-ignore] filters for the same event in a workflow.
  ///
  /// Accepts glob patterns that use characters like `*`, `**`, `+`, `?`, `!` and others to match more than one branch
  /// or tag name.
  /// If a name contains any of these characters and you want a literal match, you need to escape each of these special
  /// characters with `\`.
  ///
  /// For more information about glob patterns, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  `tags-ignore`: Listing<String>(tags == null)?

  /// Use the paths filter when you want to include file path patterns or when you want to both include and exclude file
  /// path patterns.
  ///
  /// Accepts glob patterns that use the `*` and `**` wildcard characters to match more than one path name.
  /// For more information, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  paths: Listing<String>?

  /// Use the paths-ignore filter when you only want to exclude file path patterns.
  ///
  /// You cannot use both the paths and paths-ignore filters for the same event in a workflow.
  /// If you want to both include and exclude path patterns for a single event, use the paths filter prefixed with the
  /// `!` character to indicate which paths should be excluded.
  ///
  /// Accepts glob patterns that use the `*` and `**` wildcard characters to match more than one path name.
  /// For more information, see the
  /// [Workflow syntax for GitHub Actions](https://docs.github.com/en/actions/reference/workflows-and-actions/workflow-syntax#filter-pattern-cheat-sheet).
  `paths-ignore`: Listing<String>(paths == null)?
}

class RegistryPackage extends module {
  types: Listing<ReleaseType>?
}
typealias RegistryPackageType = "published" | "updated"

class Release extends module {
  types: Listing<ReleaseType>?
}
typealias ReleaseType =
  "published" | "unpublished" | "created" | "edited" | "deleted" | "prereleased" | "released"

class RepositoryDispatch extends module {
  types: Listing<String>?
}

class Schedule {
  @SourceCode { language = "Cron" }
  cron: String
}

/// Allows workflows to be reused by other workflows.
///
/// https://docs.github.com/en/actions/learn-github-actions/events-that-trigger-workflows#workflow_call
class WorkflowCall extends module {
  inputs: Mapping<String, WorkflowInput>?
  outputs: Mapping<String, WorkflowCallOutput>?
  secrets: Mapping<String, WorkflowCallSecrets>?
}

class WorkflowCallOutput {
  description: String?
  value: Any
}
class WorkflowCallSecrets {
  description: String?
  required: Boolean
}

class WorkflowDispatch extends module {
  inputs: Mapping<String, WorkflowInput>?
}

class WorkflowInput {
  description: String
  deprecatedMessage: String?
  required: Boolean?
  default: String?
  type: WorkflowInputType?
  options: Listing<String>?
}
typealias WorkflowInputType = "boolean" | "string" | "choice" | "environment" | "number"

class WorkflowRun extends module {
  types: Listing<WorkflowRunType>?
  workflows: Listing<String>(length > 0)
  branches: Listing<String>?
  `branches-ignore`: Listing<String>?
}
typealias WorkflowRunType = "requested" | "completed" | "in_progress"
