amends "package://pkg.pkl-lang.org/github.com/stefma/pkl-gha/com.github.action@0.0.4#/GitHubAction.pkl"
import "modules/Steps.pkl"

name = "UpdatePklWorkflows"

on {
  pull_request {}
}

jobs {
  ["checkActionUpdate"] {
    `runs-on` = new UbuntuLatest {}
    `if` = "github.event.pull_request.user.login == 'dependabot[bot]'"
    outputs {
      ["shouldUpdatePklWorkflows"] = "${{ steps.dependabotMetadata.outputs.package-ecosystem == 'github-action' }}"
      ["previousDependencyName"] = "${{ steps.dependencyName.outputs.previous-dependency-name }}"
      ["newDependencyName"] = "${{ steps.dependencyName.outputs.new-dependency-name }}"
    }
    steps {
      new {
        name = "Fetch dependabot metadata"
        id = "dependabotMetadata"
        uses = "dependabot/fetch-metadata@v2"
      }
      new {
        name = "Get dependency name"
        id = "dependencyName"
        run = """
          echo '${{ steps.dependabotMetadata.outputs.updated-dependencies-json }}' > deps.json
          dependency_name=$(jq -r '.[0].dependencyName' deps.json)
          previous_version=$(jq -r '.[0].prevVersion' deps.json)
          new_version=$(jq -r '.[0].newVersion' deps.json)
          echo "previous-dependency-name=$dependency_name@v$previous_version" >> $GITHUB_OUTPUT
          echo "new-dependency-name=$dependency_name@v$new_version" >> $GITHUB_OUTPUT
          """
      }
    }
  }
  ["updatePklWorkflows"] {
    `runs-on` = new UbuntuLatest {}
    needs = "checkActionUpdate"
    `if` = "${{ needs.checkActionUpdate.outputs.shouldUpdatePklWorkflows }}"
    permissions {
      contents = "write"
    }
    steps {
      (Steps.checkout()) {
        with {
          ["ref"] = "${{ github.event.pull_request.head.ref }}"
        }
      }
      new {
        name = "Update dependencies in pkl files"
        run = """
          SEARCH_DIR=".github/pkl-workflows"
          OLD="${{ needs.checkActionUpdate.outputs.previousDependencyName }}"
          NEW="${{ needs.checkActionUpdate.outputs.newDependencyName }}"
          grep -rl -- "$OLD" "$SEARCH_DIR" | while read -r file; do
            echo "Updating $file"
            sed -i "s|$OLD|$NEW|g" "$file"
          done
          """
      }
      new {
        name = "Commit and push changes"
        run = """
          git config --global user.name "github-actions[bot]"
          git config --global user.email "41898282+github-actions[bot]@users.noreply.github.com"
          git commit -am "Update pkl workflows"
          git push origin
          """
      }
    }
  }
}