/// A Pkl template for writing GitHub Action workflows.
///
/// This module provides a set of definitions for GitHub Action workflows,
/// including triggers, jobs, steps, and environment variables.
module com.github.action.Workflow

import "Concurrency.pkl"
import "Defaults.pkl"
import "EnvironmentVariables.pkl"
import "Job.pkl"
import "Machine.pkl"
import "On.pkl"
import "Permissions.pkl"

// Jobs top-level typealias
typealias Jobs = Mapping<String(matches(Regex("[a-zA-Z_][a-zA-Z0-9_-]*"))), Job.Job>(!isEmpty)

// Templating

name: String

local const onIsSet = (on: On) ->
  on.branch_protection_rule != null
    || on.check_run != null
    || on.check_suite != null
    || on.create != null
    || on.`delete` != null
    || on.deployment != null
    || on.deployment_status != null
    || on.discussion != null
    || on.discussion_comment != null
    || on.fork != null
    || on.gollum != null
    || on.issue_comment != null
    || on.issues != null
    || on.label != null
    || on.merge_group != null
    || on.milestone != null
    || on.page_build != null
    || on.project != null
    || on.project_card != null
    || on.project_column != null
    || on.public != null
    || on.pull_request != null
    || on.pull_request_review != null
    || on.pull_request_review_comment != null
    || on.pull_request_target != null
    || on.push != null
    || on.registry_package != null
    || on.release != null
    || on.repository_dispatch != null
    || on.schedule != null
    || on.status != null
    || on.watch != null
    || on.workflow_call != null
    || on.workflow_dispatch != null
    || on.workflow_run != null

on: On(onIsSet)

/// A map of variables that are available to the steps of all jobs in the workflow.
///
/// You can also set variables that are only available to the steps of a single job or to a single step.
/// For more information, see [DefaultJob.env] and [Step.env].
///
/// When more than one environment variable is defined with the same name, GitHub uses the most specific variable.
/// For example, an environment variable defined in a step will override job and workflow environment variables with the
/// same name, while the step executes.
/// An environment variable defined for a job will override a workflow variable with the same name, while the job
/// executes.
env: EnvironmentVariables.EnvironmentVariables?

/// Use concurrency to ensure that only a single job or workflow using the same concurrency group will run at a time.
///
/// A concurrency group can be any string or expression.
/// The expression can only use
/// [`github`](https://docs.github.com/en/actions/reference/workflows-and-actions/contexts#github-context),
/// [`inputs`](https://docs.github.com/en/actions/reference/workflows-and-actions/contexts#inputs-context) and
/// [`vars`](https://docs.github.com/en/actions/reference/workflows-and-actions/contexts#vars-context) contexts.
/// For more information about expressions, see
/// [Evaluate expressions in workflows and actions](https://docs.github.com/en/actions/reference/workflows-and-actions/expressions).
///
/// You can also specify concurrency at the job level. For more information, see [BaseJob.concurrency].
/// This means that there can be at most one running and one pending job in a concurrency group at any time. When a concurrent job or workflow is queued, if another job or workflow using the same concurrency group in the repository is in progress, the queued job or workflow will be pending. Any existing pending job or workflow in the same concurrency group, if it exists, will be canceled and the new queued job or workflow will take its place.
///
/// To also cancel any currently running job or workflow in the same concurrency group, specify cancel-in-progress: true. To conditionally cancel currently running jobs or workflows in the same concurrency group, you can specify cancel-in-progress as an expression with any of the allowed expression contexts.
concurrency: Concurrency?

/// You can use permissions to modify the default permissions granted to the `GITHUB_TOKEN`, adding or removing access
/// as required, so that you only allow the minimum required access.
///
/// For more information, see
/// [Use GITHUB_TOKEN for authentication in workflows](https://docs.github.com/en/actions/tutorials/authenticate-with-github_token#permissions-for-the-github_token).
///
/// You can use permissions either as a top-level key, to apply to all jobs in the workflow, or within specific jobs.
/// When you add the permissions key within a specific job, all actions and run commands within that job that use the
/// `GITHUB_TOKEN` gain the access rights you specify.
/// For more information, see [BaseJob.permissions].
///
/// Owners of an organization can restrict write access for the `GITHUB_TOKEN` at the repository level.
/// For more information, see
/// [Disabling or limiting GitHub Actions for your organization](https://docs.github.com/en/organizations/managing-organization-settings/disabling-or-limiting-github-actions-for-your-organization#setting-the-permissions-of-the-github_token-for-your-organization).
///
/// When a workflow is triggered by the `pull_request_target` event, the `GITHUB_TOKEN` is granted read/write repository
/// permission, even when it is triggered from a public fork.
/// For more information, see
/// [Events that trigger workflows](https://docs.github.com/en/actions/reference/workflows-and-actions/events-that-trigger-workflows#pull_request_target).
permissions: (*Permissions | "read-all" | "write-all")?

/// Use defaults to create a map of default settings that will apply to all jobs
/// in the workflow. You can also set default settings that are only available to a
/// job. For more information, see jobs.<job_id>.defaults.
///
/// When more than one default setting is defined with the same name, GitHub
/// uses the most specific default setting. For example, a default setting defined
/// in a job will override a default setting that has the same name defined in
/// a workflow.
defaults: Defaults?

/// A workflow run is made up of one or more jobs, which run in parallel by default.
/// To run jobs sequentially, you can define dependencies on other jobs using the [needs][BaseJob.needs] keyword.
///
/// Each job runs in a runner environment specified by runs-on.
///
/// You can run an unlimited number of jobs as long as you are within the workflow usage limits.
/// For more information, see [Billing and usage](https://docs.github.com/en/actions/concepts/billing-and-usage) for
/// GitHub-hosted runners and [Actions limits](https://docs.github.com/en/actions/reference/limits) for self-hosted
/// runner usage limits.
///
/// If you need to find the unique identifier of a job running in a workflow run, you can use the GitHub API.
///
/// For more information, see
/// [REST API endpoints for GitHub Actions](https://docs.github.com/en/rest/actions?apiVersion=2022-11-28#workflow-jobs).
jobs: Jobs

// Output
output {
  text =
    "# Do not modify!\n# This file was generated from a template using https://github.com/StefMa/pkl-gha\n\n\(super.text)"
  renderer = new YamlRenderer {
    converters {
      ["runs-on"] = (runsOn: String | Machine | Listing<String | Machine>) ->
        if (runsOn is Listing<String | Machine>)
          convertMachinesToString(runsOn)
        else
          convertMachineToString(runsOn)
    }
  }
}

local const function convertMachinesToString(runsOn: Listing<String | Machine>): Listing<String> =
  runsOn.toList().map((type) -> convertMachineToString(type)).toListing()
local const function convertMachineToString(machine: String | Machine): String =
  if (machine is Machine) machine.name else machine
